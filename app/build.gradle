plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'de.mannodermaus.android-junit5'
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--count'
            standardOutput = stdout
        }
        return Integer.valueOf(stdout.toString().trim())
    } catch (ignored) {
        return null
    }
}

android {
    namespace 'de.bahnhoefe.deutschlands.bahnhofsfotos'

    compileOptions {
        // use the diamond operator and some other goodies in Android
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
        applicationId "de.bahnhoefe.deutschlands.bahnhofsfotos"
        compileSdk 34
        minSdk 26
        targetSdk 34
        versionCode 84
        versionName "14.7.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        // by convention, the folder name "app" is used for the APK file name. we want bahnhofsfotos instead
        archivesBaseName = "bahnhofsfotos"
    }
    signingConfigs {
        nightly {
            if (System.getProperty("nightly_store_file") != null) {
                storeFile file(System.getProperty("nightly_store_file"))
                storePassword System.getProperty("nightly_store_password")
                keyAlias System.getProperty("nightly_key_alias")
                keyPassword System.getProperty("nightly_key_password")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        nightly {
            signingConfig signingConfigs.nightly
            applicationIdSuffix ".nightly"
        }
    }

    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/*']
        }
    }

    lint {
        abortOnError false
    }

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "applicationId", variant.applicationId

        if (variant.buildType.name == 'nightly') {
            variant.outputs.configureEach {
                setVersionCodeOverride(getVersionCode())
                setVersionNameOverride(versionName + "-" + versionCode)
                outputFileName = "${applicationId}_${variant.versionCode}.apk"
            }
        }
    }
}

ext {
    mapsforgeVersion = '0.20.0'
    retrofitVersion = '2.9.0'
    junitVersion = '5.10.0'
    assertJVersion = '3.24.2'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Desugaring
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.3"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:4.11.0"
    implementation "org.mapsforge:mapsforge-map-reader:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-core:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-themes:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-map:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-map-android:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-map-android:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-map-reader:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-themes:$mapsforgeVersion"
    implementation "net.sf.kxml:kxml2:2.3.0"
    implementation "com.caverock:androidsvg:1.4"
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'commons-io:commons-io:20030203.000550'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.browser:browser:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.mcxiaoke.volley:library:1.0.19'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.11.0'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'de.mannodermaus.junit5:android-test-core:1.3.0'
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    androidTestImplementation "org.assertj:assertj-core:$assertJVersion"
    androidTestRuntimeOnly 'de.mannodermaus.junit5:android-test-runner:1.3.0'

}
